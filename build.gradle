plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
    id 'de.jjohannes.extra-java-module-info' version "0.14"
}

group 'cf.thdisstudio'
version '1.0.0'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
mainClassName = 'cf.thdisstudio.audio_downloader.AudioDownloaderApplication'
application {
    mainModule = 'cf.thdisstudio.audio_downloader'
    mainClass = 'cf.thdisstudio.audio_downloader.AudioDownloaderApplication'
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.media']
}

dependencies {
    implementation 'org.openjfx:javafx-media:17'
    implementation 'org.json:json:20220320'
    implementation 'com.github.sealedtx:java-youtube-downloader:3.1.0'
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {

    mergedModule {
        requires 'java.sql';
        requires 'java.xml';
        requires 'java.desktop';
    }

    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'YSMD'
    }

    jpackage {
        installerOptions += '--verbose'
    }
}

jlinkZip {
    group = 'distribution'
}

extraJavaModuleInfo {
    failOnMissingModuleInfo.set(false)
    automaticModule("java-youtube-downloader-3.1.0.jar", "java.youtube.downloader")
}

jar {
    manifest {
        attributes 'Main-Class': 'cf.thdisstudio.audio_downloader.AudioDownloaderApplication'
    }
}